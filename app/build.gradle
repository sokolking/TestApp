plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

apply from: '../config.gradle'

android {

    compileSdkVersion project.compileSdk
    buildToolsVersion project.buildTools

    defaultConfig {
        applicationId project.appId
        minSdkVersion project.minSdk
        targetSdkVersion project.targetSdk
        versionCode project.vCode
        versionName project.vName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        canary {
            initWith debug
            matchingFallbacks = ['debug']
        }
    }
    flavorDimensions "environment"
    productFlavors {
        prod {
            dimension "environment"
            buildConfigField("String", "API", "\"https://jsonplaceholder.typicode.com/\"")
        }
        dev {
            dimension "environment"
            buildConfigField("String", "API", "\"https://jsonplaceholder.typicode.com/\"")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {

    //modules
    implementation project(":base")
    implementation project(":navigation")
    implementation project(":domain")
    implementation project(":api")
    implementation project(":i_comment")

    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesAndroidVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$kotlixLifecycleExtVersion"
    implementation "androidx.core:core-ktx:$kotlixExtVersion"

    //app compat
    implementation "com.google.android.material:material:$xMaterial"
    implementation "androidx.appcompat:appcompat:$xVersion"
    implementation "androidx.constraintlayout:constraintlayout:$xConstraint"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$xSwipeRefresh"
    implementation "androidx.fragment:fragment-ktx:$xFragment"

    //koin
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-android-scope:$koinVersion"
    implementation "org.koin:koin-android-viewmodel:$koinVersion"

    //multidex
    implementation "androidx.multidex:multidex:$xMultidex"

    //coil
    implementation "io.coil-kt:coil:$coilVersion"

    //canary
    canaryImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}